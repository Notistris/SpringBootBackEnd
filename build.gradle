plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.notistris'
version = '0.0.1-SNAPSHOT'
description = 'Identity-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.test {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport) // sinh report sau khi chạy test
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            '**/*Application.class',
                            'com/notistris/identityservice/dto/**',
                            'com/notistris/identityservice/entity/**',
                            'com/notistris/identityservice/mapper/**',
                            'com/notistris/identityservice/config/**',
                            'com/notistris/identityservice/enums/**'
                    ])
                })
        )
    }

}

spotless {
    java {
        removeUnusedImports()
        toggleOffOn()
        trimTrailingWhitespace()
        endWithNewline()
        indentWithTabs(4)
        palantirJavaFormat()

        // Thứ tự import
        importOrder('java', 'jakarta', 'org', 'com', 'com.diffplug')

        target 'src/**/*.java'
    }
    // Nếu có code Kotlin
    kotlin {
        ktlint()
        target 'src/**/*.kt'
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // Web dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Lombok dependencies
    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'


    // Mapstruct dependencies
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // Lombok - Mapstruct binding
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // Security dependencies
    implementation 'org.springframework.security:spring-security-crypto:6.5.3'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server:3.5.5'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // Test dependencies
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.mapstruct:mapstruct:1.6.3'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // https://mvnrepository.com/artifact/com.h2database/h2
    testImplementation("com.h2database:h2:2.3.232")

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    testImplementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0")
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation platform("org.testcontainers:testcontainers-bom:1.21.3")
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mariadb"

}
